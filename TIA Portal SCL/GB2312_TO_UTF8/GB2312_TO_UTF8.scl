FUNCTION_BLOCK "GB2312_TO_UTF8"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      GB2312 : String[254];
   END_VAR

   VAR_OUTPUT 
      UTF8 : String[254];
   END_VAR

   VAR 
      g_u { S7_SetPoint := 'False'} : "GB2_TO_UNI";
      u_u8 : "UNI_TO_UTF8";
   END_VAR

   VAR_TEMP 
      i : UDInt;
      j : UDInt;
      lenGB2 : Int;
      tempword : Word;
      tempstr : String[4];
   END_VAR


BEGIN
	#UTF8 := '';
	#lenGB2 := LEN(#GB2312);
	IF #lenGB2 > 0 THEN
	    #j := 1;
	    FOR #i := 1 TO INT_TO_UDINT(#lenGB2) DO
	        IF #i = #j THEN
	            IF BYTE_TO_WORD(#GB2312[#i]) < 16#0080 THEN
	                #tempword := BYTE_TO_WORD(#GB2312[#i]);
	                #j := #j + 1;
	            ELSE
	                #tempword := BYTE_TO_WORD(#GB2312[#i]);
	                #tempword := SHL(IN := #tempword, N := 8);
	                #tempword := #tempword OR BYTE_TO_WORD(#GB2312[#i + 1]);
	                #j := #j + 2;
	            END_IF;
	            #g_u(GB2312 := #tempword,
	                 UNICODE => #tempword);
	            #u_u8(UNICODE := #tempword,
	                  UTF8 => #tempstr);
	            #UTF8 := CONCAT(IN1 := #UTF8, IN2 := #tempstr);
	        END_IF;
	    END_FOR;
	END_IF;
END_FUNCTION_BLOCK

